! Annotated Master file for Area 4 of the IEEE 123-bus test case.
!
! This file is meant to be invoked from the Compile command in the "Run_IEEE123Bus.DSS"  file.
! 
! Note: DSS commands, property names, etc., are NOT case sensitive. Capitalize as you please.
! You should always do a "Clear" before making a new circuit:

Clear

! INSTANTIATE A NEW CIRCUIT AND DEFINE A STIFF 4160V SOURCE
! The new circuit is called "ieee123"
! This creates a Vsource object connected to "sourcebus". This is now the active circuit element, so
! you can simply continue to edit its property value. 
! The basekV is redefined to 4.16 kV. The bus name is changed to "150" to match one of the buses in the test feeder.
! The source is set for 1.0 per unit and the Short circuit impedance is set to a small value (0.0001 ohms)
! The ~ is just shorthad for "more" for the New or Edit commands

New object=circuit.ieee123_area_4
~ basekv=4.16 Bus1=60 pu=1.00 R1=0 X1=0.0001 R0=0 X0=0.0001

! REDIRECT INPUT STREAM TO FILE CONTAINING DEFINITIONS OF LINECODES
! This file defines the line impedances is a similar manner to the description in the test case.

Redirect        IEEELinecodes.DSS

! LINE DEFINITIONS
! Lines are defined by referring to a "linecode" that contains the impedances per unit length
! So the only properties required are the LineCode name and Length.  Units are assumed to match the definition
! since no units property is defined in either the Linecodes file or this file.
! Note that it is not necessary to explicitly specify the node connections for the 3-phase lines
! unless they are not ".1.2.3". However, they are spelled out here for clarity.
! The DSS assumes .1.2.3.0.0 ... for connections of 3 or more phases.
! Likewise, .1 is not necessary for 1-phase lines connected to phase 1. However, if it is connected
! to any other phase, it must be specified. For completeness, everything is spelled out here.
!
! Note that it is recommended that the "units=" property be used here and in the Linecode definition as well
! to avoid confusion in the future

New Line.L66    Phases=1 Bus1=67.1       Bus2=68.1       LineCode=9    Length=0.2
New Line.L67    Phases=3 Bus1=67.1.2.3   Bus2=72.1.2.3   LineCode=3    Length=0.275
New Line.L68    Phases=3 Bus1=67.1.2.3   Bus2=97.1.2.3   LineCode=3    Length=0.25
New Line.L69    Phases=1 Bus1=68.1       Bus2=69.1       LineCode=9    Length=0.275
New Line.L70    Phases=1 Bus1=69.1       Bus2=70.1       LineCode=9    Length=0.325
New Line.L71    Phases=1 Bus1=70.1       Bus2=71.1       LineCode=9    Length=0.275
New Line.L72    Phases=1 Bus1=72.3       Bus2=73.3       LineCode=11   Length=0.275
New Line.L73    Phases=3 Bus1=72.1.2.3   Bus2=76.1.2.3   LineCode=3    Length=0.2
New Line.L74    Phases=1 Bus1=73.3       Bus2=74.3       LineCode=11   Length=0.35
New Line.L75    Phases=1 Bus1=74.3       Bus2=75.3       LineCode=11   Length=0.4
New Line.L76    Phases=3 Bus1=76.1.2.3   Bus2=77.1.2.3   LineCode=6    Length=0.4
New Line.L77    Phases=3 Bus1=76.1.2.3   Bus2=86.1.2.3   LineCode=3    Length=0.7
New Line.L78    Phases=3 Bus1=77.1.2.3   Bus2=78.1.2.3   LineCode=6    Length=0.1
New Line.L79    Phases=3 Bus1=78.1.2.3   Bus2=79.1.2.3   LineCode=6    Length=0.225
New Line.L80    Phases=3 Bus1=78.1.2.3   Bus2=80.1.2.3   LineCode=6    Length=0.475
New Line.L81    Phases=3 Bus1=80.1.2.3   Bus2=81.1.2.3   LineCode=6    Length=0.175
New Line.L82    Phases=3 Bus1=81.1.2.3   Bus2=82.1.2.3   LineCode=6    Length=0.25
New Line.L83    Phases=1 Bus1=81.3       Bus2=84.3       LineCode=11   Length=0.675
New Line.L84    Phases=3 Bus1=82.1.2.3   Bus2=83.1.2.3   LineCode=6    Length=0.25
New Line.L85    Phases=1 Bus1=84.3       Bus2=85.3       LineCode=11   Length=0.475
New Line.L86    Phases=3 Bus1=86.1.2.3   Bus2=87.1.2.3   LineCode=6    Length=0.45
New Line.L87    Phases=1 Bus1=87.1       Bus2=88.1       LineCode=9    Length=0.175
New Line.L88    Phases=3 Bus1=87.1.2.3   Bus2=89.1.2.3   LineCode=6    Length=0.275
New Line.L89    Phases=1 Bus1=89.2       Bus2=90.2       LineCode=10   Length=0.25
New Line.L90    Phases=3 Bus1=89.1.2.3   Bus2=91.1.2.3   LineCode=6    Length=0.225
New Line.L91    Phases=1 Bus1=91.3       Bus2=92.3       LineCode=11   Length=0.3
New Line.L92    Phases=3 Bus1=91.1.2.3   Bus2=93.1.2.3   LineCode=6    Length=0.225
New Line.L93    Phases=1 Bus1=93.1       Bus2=94.1       LineCode=9    Length=0.275
New Line.L94    Phases=3 Bus1=93.1.2.3   Bus2=95.1.2.3   LineCode=6    Length=0.3
New Line.L95    Phases=1 Bus1=95.2       Bus2=96.2       LineCode=10   Length=0.2
New Line.L96    Phases=3 Bus1=97.1.2.3   Bus2=98.1.2.3   LineCode=3    Length=0.275
New Line.L97    Phases=3 Bus1=98.1.2.3   Bus2=99.1.2.3   LineCode=3    Length=0.55
New Line.L98    Phases=3 Bus1=99.1.2.3   Bus2=100.1.2.3  LineCode=3    Length=0.3
New Line.L99    Phases=3 Bus1=100.1.2.3  Bus2=450.1.2.3  LineCode=3    Length=0.8
New Line.L117   Phases=3 Bus1=160r.1.2.3 Bus2=67.1.2.3   LineCode=6    Length=0.35

New Load.S68a  Bus1=68.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S69a  Bus1=69.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S70a  Bus1=70.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S71a  Bus1=71.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S73c  Bus1=73.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S74c  Bus1=74.3   Phases=1 Conn=Wye   Model=2 kV=2.4   kW=40.0  kvar=20.0  
New Load.S75c  Bus1=75.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S76a  Bus1=76.1   Phases=1 Conn=Wye   Model=5 kV=2.4   kW=105.0 kvar=80.0  
New Load.S76b  Bus1=76.2   Phases=1 Conn=Wye   Model=5 kV=2.4   kW=70.0  kvar=50.0  
New Load.S76c  Bus1=76.3   Phases=1 Conn=Wye   Model=5 kV=2.4   kW=70.0  kvar=50.0  
New Load.S77b  Bus1=77.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S79a  Bus1=79.1   Phases=1 Conn=Wye   Model=2 kV=2.4   kW=40.0  kvar=20.0  
New Load.S80b  Bus1=80.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S82a  Bus1=82.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S83c  Bus1=83.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S84c  Bus1=84.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S85c  Bus1=85.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S86b  Bus1=86.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S87b  Bus1=87.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S88a  Bus1=88.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S90b  Bus1=90.2   Phases=1 Conn=Wye   Model=5 kV=2.4   kW=40.0  kvar=20.0  
New Load.S92c  Bus1=92.3   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S94a  Bus1=94.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S95b  Bus1=95.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S96b  Bus1=96.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=20.0  kvar=10.0  
New Load.S98a  Bus1=98.1   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S99b  Bus1=99.2   Phases=1 Conn=Wye   Model=1 kV=2.4   kW=40.0  kvar=20.0  
New Load.S100c Bus1=100.3  Phases=1 Conn=Wye   Model=2 kV=2.4   kW=40.0  kvar=20.0   

! NORMALLY CLOSED SWITCHES ARE DEFINED AS SHORT LINES
! Could also be defned by setting the Switch=Yes property

New Line.Sw4    phases=3  Bus1=60     Bus2=160    switch=True r1=0.001 r0=0.001 x1=0.001 x0=0.001 C1=0 C0=0 length=0.001 

! CAPACITORS
! Capacitors are 2-terminal devices. The 2nd terminal (Bus2=...) defaults to all phases
! connected to ground (Node 0). Thus, it need not be specified if a Y-connected or L-N connected
! capacitor is desired

!New Capacitor.C83       Bus1=83      Phases=3     kVAR=600     kV=4.16
!New Capacitor.C88a      Bus1=88.1    Phases=1     kVAR=50      kV=2.402
!New Capacitor.C90b      Bus1=90.2    Phases=1     kVAR=50      kV=2.402
!New Capacitor.C92c      Bus1=92.3    Phases=1     kVAR=50      kV=2.402

New Storage.Battery4 phases=1 bus1=86.2 kVA=150 kV=2.4
~ kWrated=150 kWHrated=500 %reserve=30 %stored=75
~ state=charge kW=-0.0 PF=1

New "PVSystem.dg_54" bus1=68.1 phases=1 kv=2.4 Pmpp=130 kVA=130 pf=1 irradiance=1 %Cutin=0.1 %Cutout=0.1
New "PVSystem.dg_60" bus1=75.3 phases=1 kv=2.4 Pmpp=260 kVA=260 pf=1 irradiance=1 %Cutin=0.1 %Cutout=0.1
New "PVSystem.dg_66" bus1=80.2 phases=1 kv=2.4 Pmpp=260 kVA=260 pf=1 irradiance=1 %Cutin=0.1 %Cutout=0.1
New "PVSystem.dg_72" bus1=87.3 phases=1 kv=2.4 Pmpp=280 kVA=280 pf=1 irradiance=1 %Cutin=0.1 %Cutout=0.1
New "PVSystem.dg_78" bus1=96.2 phases=1 kv=2.4 Pmpp=150 kVA=150 pf=1 irradiance=1 %Cutin=0.1 %Cutout=0.1

new transformer.reg4a phases=1          windings=2        buses=[160.1 160r.1]   conns=[wye wye]       kvs=[2.402 2.402] kvas=[2000 2000] XHL=.01 %LoadLoss=0.00001 ppm=0.0 
new transformer.reg4b like=reg4a                          buses=[160.2 160r.2]      ppm=0.0 
new transformer.reg4c like=reg4a                          buses=[160.3 160r.3]      ppm=0.0

! POINT REGULATOR CONTROLS TO REGULATOR TRANSFORMER AND SET PARAMETERS 
new regcontrol.creg4a               transformer=reg4a winding=2         vreg=124     band=2  ptratio=20 ctprim=300 R=0.6   X=1.3  
new regcontrol.creg4b like=creg4a   transformer=reg4b                                                              R=1.4   X=2.6   
new regcontrol.creg4c like=creg4a   transformer=reg4c                                                              R=0.2   X=1.4   

! NOTE: WHEN LIKE= IS USED, IT IS NECESSARY TO SPECIFY ONLY THOSE PROPERTIES THAT ARE DIFFERENT

Set VoltageBases = [4.16, 0.48]    ! ARRAY OF VOLTAGES IN KV
CalcVoltageBases  ! PERFORMS ZERO LOAD POWER FLOW TO ESTIMATE VOLTAGE BASES

solve